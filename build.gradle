// Adds empty build, clean, and assemble tasks to project
plugins {
    id 'base'
    id 'org.hidetake.ssh' version '2.9.0'
}

allprojects {
    // This is where you select which operating system to build for.
    // NOTE: If you do a build on Windows targeting another architecture, tests
    // will (naturally) fail.  Therefore, you can do:
    // gradlew build -x test

    // TODO: Can we factor this out to the command line...and also select which
    // remote definition to deploy to from the command line?

    // Uncomment the line below to build for windows
    //ext.buildType = "windows"

    // Uncomment the line below to build for a Raspberry Pi running raspbian
    ext.buildType = "arm-raspbian"

    // Uncomment the line below to build for an armhf device such as a Jetson or a Beaglebone Black
    //ext.buildType = "armhf"

    ext.outputDirectory = file("${rootDir}/output")
}

remotes {
    piccb {
        host = 'visioncoproc.local'
        user = 'pi'
        password = 'raspberry'
    }
}

ssh.settings {
    knownHosts = allowAnyHosts
    fileTransfer = 'scp'
}

// Normally this is implicit with java plugin, but base plugin makes no sub-project assumptions
build.dependsOn ':CameraServer:build', ':CameraVision:build', ':NTSimulator:build'

task copyStartupScript(type: Copy) {
  description "Copies the startup application to the root output directory."
  destinationDir = outputDirectory
  if (buildType == "windows") {
    from (file("startup.bat")) {
    }
    from (file("local-debug-startup.bat")) {
    }
  } else {
    from (file("startup.sh")) {
    }
    from (file("startup-debug.sh")) {
    }
    from (file("local-debug-startup.sh")) {
    }
  }
}

build.dependsOn copyStartupScript

clean {
    inputs.dir outputDirectory
    description "Remove the root output directory."
    delete outputDirectory
}

// Make the deploy task accept a parameter that will tell below which remote
// to deploy to.
task deploy(dependsOn: build) {
    doLast {
        if (buildType == "windows") {
            println "Windows deployment not coded at this time."
        } else {
            ssh.run {
                session(remotes.piccb) {
                    // Install dependencies
                    // This should bring up project from a fresh raspbian install.
                    // You must have ssh installed first using `sudo raspi-config`.
                    // Will this work on the robo-rio?
                    // Doubt it...don't know...must test.
                    executeSudo 'apt-get --assume-yes update'
                    executeSudo 'apt-get --assume-yes install dos2unix'
                    executeSudo 'apt-get --assume-yes install python3'
                    executeSudo 'apt-get --assume-yes install python3-pip'
                    executeSudo 'apt-get --assume-yes install oracle-java8-jdk'
                    execute 'pip3 install -U pynetworktables'
                    // Now copy the build
                    put from: 'output', into: '/home/pi'
                    execute 'chmod u+x /home/pi/output/*.sh'
                    execute 'dos2unix /home/pi/output/*.sh'
                }
            }
        }
    }
}